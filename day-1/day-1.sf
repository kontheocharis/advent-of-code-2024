-- UTILS

#unfold
def return : [A : Type] -> A -> IO A {
  io-return
}

#unfold
def bind : [A B : Type] -> IO A -> (A -> IO B) -> IO B {
  io-bind
}

def lines : String -> List String {
  \s => filter (\x => not (string-eq x "")) (string-split s "\n")
}

def words : String -> List String {
  \s => filter (\x => not (string-eq x "")) (string-split s " ")
}

def first-two : [A : Type] -> List A -> Maybe (A * A) {
  \case {
    [] => nothing,
    [x, ..xs] => case xs {
      [] => nothing,
      [y, .._] => just (x, y)
    }
  }
}

def forM : [A B : Type] -> List A -> (A -> IO B) -> IO (List B) {
  \l f => case l {
    [] => return [],
    [x, ..xs] => {
      let y <- f x;
      let ys <- forM xs f;
      return [y, ..ys]
    }
  }
}

def parse-natM : String -> IO Nat {
  \s => case parse-nat s {
    nothing => throw (string-concat "Invalid input: " s),
    just n => return n
  }
}

def zip : [A B : Type] -> List A -> List B -> List (A * B) {
  \l r => case l {
    [] => [],
    [x, ..xs] => case r {
      [] => [],
      [y, ..ys] => [(x, y), ..zip xs ys]
    }
  }
}

def unzip : [A B : Type] -> List (A * B) -> (List A * List B) {
  \case {
    [] => ([], []),
    [m, ..xs] => {
      let (x, y) = m;
      let (l, r) = unzip xs;
      ([x, ..l], [y, ..r])
    }
  }
}

def sum : List Nat -> Nat {
  fold 0 (\x acc => add x acc)
}

def distance : Nat -> Nat -> Nat {
  \x y => {
    if lt x y {
      sub y x
    } else {
      sub x y
    }
  }
}

def zip-with : [A B C : Type] -> (A -> B -> C) -> List A -> List B -> List C {
  \f l r => {
    map (\x => {
      let (a, b) : A * B = x;
      f a b
    }) (zip l r)
  }
}

-- PROGRAM

def similarity-score : List Nat -> List Nat -> Nat {
  \l r => sum (map (\x => mul x (length (filter (nat-eq x) r))) l)
}

def list-distance : List Nat -> List Nat -> Nat {
  \l r => sum (zip-with (distance) (sort nat-cmp l) (sort nat-cmp r))
}

def main : IO () {
  let txt <- input;

  let numbers <- forM (lines txt) (\line => {
    case first-two (words line) {
      nothing => throw "Invalid input",
      just ws => {
        let (a, b) = ws;
        let a' <- parse-natM a;
        let b' <- parse-natM b;
        return (a', b')
      }
    }
  });

  let (l, r) = unzip numbers;

  do print "Part 1:";
  let part1 = list-distance l r;
  do print (show-nat part1);

  do print "Part 2:";
  let part2 = similarity-score l r;
  do print (show-nat part2);

  return ()
}
